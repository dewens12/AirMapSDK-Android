apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.airmap.airmapsdk'
def libVersion = '2.0.1'

android {
    compileSdkVersion 29
 //   buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 1
        versionName "$libVersion"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "@string/com_auth0_scheme"]

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '30.0.0 rc2'
}

dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation 'org.robolectric:robolectric:4.3.1'

    // support
    // def supportLibraryVersion = '27.1.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.fragment:fragment:1.2.4'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.1.0'

    // play services
    // def playServicesVersion = '17.0.0'
    implementation "com.google.android.gms:play-services-location:17.0.0"

    // mapbox
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-locationlayer:0.11.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-services:2.2.10'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-ui:2.2.10'

    // shared libraries
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.1'
    implementation 'online.devliving:securedpreferencestore:0.7.4'
    // Newer version available, but updating requires making some api changes within our code
    implementation 'com.auth0.android:lock:2.18.0'

    // other
    implementation 'com.squareup.okhttp3:okhttp:4.5.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.bitbucket.b_c:jose4j:0.7.0'
    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.1.0'
    implementation 'joda-time:joda-time:2.10.5'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.3'
    implementation 'com.auth0.android:auth0:1.23.0'
}

configurations {
    // to avoid double inclusion of support libraries
    all*.exclude group: 'com.android.support', module: 'support-v4'
}

def siteUrl = 'http://www.airmap.com'
def projectName = 'com.airmap.airmapsdk'
def gitUrl = 'https://github.com/airmap/AirMapSDK-Android.git'

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        userOrg = 'airmapio'
        version libVersion
        name = projectName
        desc = 'Create Flights, Send Telemetry Data, Get Realtime Traffic Alerts.'
        licenses = ['MIT']
        labels = ['android', 'airmap', 'drone', 'airspace', 'sdk', 'aar']
        vcsUrl = gitUrl
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name projectName
            url siteUrl
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/airmap/AirMapSDK-Android/blob/master/LICENSE'
                }
            }
            developers {
                developer {
                    id 'collin'
                    name 'Collin Vance'
                    email 'collin@airmap.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
